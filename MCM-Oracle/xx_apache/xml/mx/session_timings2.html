% $r->content_type('application/json');
<% $json_string %>
<%init>
my @timings = (); my $total_elapsed = 0; my $total_cpu = 0; my $total_rdb = 0;

if ( $hostname && $pid && $mx_pid ) {
  my $file = $config->MXENV_ROOT . '/mxtiming_' . $mx_pid . '_' . $hostname . '_' . $pid . '.log';
  if ( my $fh = IO::File->new( $file, '<' ) ) {
    while ( my $line = <$fh> ) {
      if ( $line =~ /^\d{8}\|/ ) {
        my @fields = split /\|/, $line;
        my ($date, $time, $id, $context, $command, $elapsed, $cpu, $rdb) = @fields[0,1,3,4,5,6,7,9];
        $context =~ s/\s*$//;
        $command =~ s/\s*$//;
        $elapsed =~ s/s$//;
        $cpu     =~ s/s$//;
        $rdb     =~ s/s$//;

        push @timings, [ $time, $id, $context, $command, $elapsed, $cpu, $rdb ];

        unless ( $context =~ /sessioncreate/i or $context =~ /sessionkill/i or $context =~ /requestdocument/i ) {
          $total_elapsed += $elapsed;
          $total_cpu     += $cpu;
          $total_rdb     += $rdb;
        }
      }
    }

    $fh->close;
  }
}
elsif ( $session_id ) {
  my @results = $db_audit->retrieve_timings( session_id => $session_id );

  foreach my $result ( @results ) {
    if ( $result->[2] ne 'Totals' ) {
      $result->[0] = Mx::Util->convert_time_short( $result->[0] );
      push @timings, $result;
    }
    else {
      $total_elapsed = $result->[4];
      $total_cpu     = $result->[5];
      $total_rdb     = $result->[6];
    }
  }
}

$total_elapsed = sprintf "%.2f", $total_elapsed;
$total_cpu     = sprintf "%.2f", $total_cpu;
$total_rdb     = sprintf "%.2f", $total_rdb;

my $coder = JSON::XS->new->allow_blessed->convert_blessed;
my $json_string = $coder->encode( { timings => \@timings, total_elapsed => $total_elapsed, total_cpu => $total_cpu, total_rdb => $total_rdb } );
</%init>
<%args>
$hostname => ''
$pid => ''
$mx_pid => ''
$session_id => ''
</%args>
